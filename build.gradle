plugins {
    id "java-library"
    id "java-gradle-plugin"
    id "com.gradle.plugin-publish" version "2.0.0"
    id "pl.allegro.tech.build.axion-release" version "1.16.1"
    id "com.vanniktech.maven.publish" version "0.26.0"
}

scmVersion { tag { prefix.set("") } }

project.version = scmVersion.version
project.group = 'de.esoco.gwt'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation "com.google.guava:guava:23.5-jre"
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5', gradleTestKit()
}

processResources {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

publishing {
    publications {
        pluginPublication(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}

gradlePlugin {
    website = 'https://github.com/esoco/gwt-gradle-plugin'
    vcsUrl = 'https://github.com/esoco/gwt-gradle-plugin.git'
    plugins {
        create("gwtPlugin") {
            id = 'de.esoco.gwt'
            implementationClass = 'de.esoco.gwt.gradle.GwtPlugin'
            displayName = 'Gradle GWT plugin'
            description = 'GWT Application Gradle Plugin'
            tags.set(['gwt', 'java'])
        }
        create("gwtLibPlugin") {
            id = 'de.esoco.gwt-lib'
            implementationClass = 'de.esoco.gwt.gradle.GwtLibPlugin'
            displayName = 'Gradle GWT lib plugin'
            description = 'GWT Library Gradle Plugin'
            tags.set(['gwt', 'java'])
        }

    }
}

mavenPublishing {
    coordinates("$project.group", "$project.name", "$project.version")

    pom {
        name = "$project.name"
        description = "$project.description"
        url = "https://github.com/esoco/${project.name}"
        licenses {
            license {
                name = "LGPL-3.0 license"
                url = "https://www.gnu.org/licenses/lgpl-3.0.html.en#license-text"
                distribution = "https://www.gnu.org/licenses/lgpl-3.0.html.en"
            }
        }
        developers {
            developer {
                id = "esoco"
                name = "esoco GmbH"
                url = "https://esoco.de/"
            }
        }
        scm {
            url = "https://github.com/esoco/${project.name}/"
            connection = "scm:git:git://github.com/esoco/${project.name}.git"
            developerConnection = "scm:git:ssh://git@github.com/esoco/${project.name}.git"
        }
    }
}